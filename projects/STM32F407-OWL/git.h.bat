@echo off

set output_filename=git.h

cd %~dp0

echo Generating %~dp0%output_filename%

echo #ifndef __GIT_H__> %output_filename%
echo #define __GIT_H__>> %output_filename%

echo.>> %output_filename%
echo // This macro is generated by batch script, do not modify anything here.>> %output_filename%

if "%~1"=="clear" (call :SET_UNDEFINED) else (call :GET_COMMIT)

set HEAD_QUOTE=^"%HEAD%^"
echo|set /p=" #define GIT_COMMIT_HEAD ">> %output_filename%
echo %HEAD_QUOTE%>> %output_filename%
echo.>> %output_filename%

echo #endif>> %output_filename% 

goto :EOF


:GET_COMMIT
	for /F "tokens=*" %%a in ('git rev-parse --short HEAD') do SET HEAD=%%a
	echo Git HEAD = %HEAD%
	exit /b

:SET_UNDEFINED
	SET HEAD=UNDEFINED
	echo Git HEAD cleared.
	exit /b



